require "shellfish/ui";
require "./CodePad.shui";
require "./FilesBox.shui";
require "./GridLayout.shui";
require "./NotificationArea.shui";
require "./RegistryDialog.shui";
require "./TimeLabel.shui";
require "./youtubefs.js";
require "/icons/comm-icons.css";
require "/icons/fs-icons.css";
require "/icons/media-icons.css";
require "/icons/ui-icons.css";

Document {
    id: doc

    property logInPending: true
    property loggedIn: false
    property user: ""

    // a bag of files for copy/move operations
    property filesBag: []
    property filesBagSource: null

    property darkMode: true //systemDarkMode
    property darkTheme: DarkTheme { contentBackgroundColor: colorName("black") }
    property lightTheme: Theme { }
    theme: darkMode ? darkTheme : lightTheme

    property showStatusBar: true

    color: theme.primaryBackgroundColor

    onContextMenu: ev => { ev.accepted = true; }

    onInitialization: () =>
    {
        if (window.localStorage)
        {
            window.localStorage.setItem("shellfish-use-cache", "true");
        }

        remoteFs.list("/")
        .then(f =>
        {
            // already logged in
            logInPending = false;
            loggedIn = true;
        })
        .catch(err =>
        {
            // not logged in yet
            logInPending = false;
        });
    }

    onKeyDown: ev =>
    {
        // dump debug information
        if (ev.key === "d" && ev.ctrlKey)
        {
            console.log("Objects Dump:");
            console.log(core.dumpStatus());
            ev.accepted = true;
        }
    }

    onLoggedInChanged: () =>
    {
        if (loggedIn)
        {
            // initialize
            const req = httpRequest("/whoami").send()
            .then(req => req.text())
            .then(data =>
            {
                doc.user = data;
            });
        }
    }

    RpcProxy {
        id: proxy

        endpoint: "/::rpc"
    }

    History {
        id: history

        property scopes: []

        onLoad: nil =>
        {
            defer(() =>
            {
                scopes.pop();
            });
        }
    }

    DavFS {
        id: remoteFs
    }

    YouTubeFS {
        id: youTubeFs
    }

    OfflineFS {
        id: offlineFs
        volume: "offline"
    }

    // main screen
    Box {
        //visible: doc.loggedIn

        fillWidth: true
        height: documentRoot.windowHeight
        color: theme.contentBackgroundColor

        // status bar
        MouseBox {
            fillWidth: true
            height: theme.itemHeightMedium
            color: theme.contentBackgroundColor
            layout: "center-row"

            visible: doc.showStatusBar

            // menu button
            Button {
                fillHeight: true
                flat: true
                icon: "ui-menu"

                onClick: (ev) =>
                {
                    ev.accepted = true;
                    const menu = menuT();
                    menu.popup(self);
                }

                property menuT: template Menu {

                    /*
                    MenuItem {
                        icon: "fs-folder"
                        text: "File Browser"

                        onClick: () =>
                        {
                            windowsModel.insert(windowsModel.size, {
                                component: filesBoxT,
                                properties: {
                                    fs: remoteFs,
                                    path: "/"
                                }
                            });
                        }
                    }

                    MenuItem {
                        icon: "media-movie"
                        text: "YouTube Browser"

                        onClick: () =>
                        {
                            windowsModel.insert(windowsModel.size, {
                                component: filesBoxT,
                                properties: {
                                    fs: youTubeFs,
                                    path: "/",
                                    searchBox: true
                                }
                            });
                        }
                    }

                    MenuSeparator { }
                    */

                    MenuItem {
                        text: doc.darkMode ? "Bright Mode"
                                           : "Dark Mode"
                        onClick: () =>
                        {
                            doc.darkMode = ! doc.darkMode;
                        }
                    }

                    MenuItem {
                        text: "Registry Editor..."
                        onClick: () =>
                        {
                            proxy.invoke("registry")
                            .then(regObj =>
                            {
                                console.log("GOT REGISTRY");
                                const dlg = doc.registryDialogT();
                                dlg.registry = regObj;
                                dlg.show();
                            })
                            .catch(err => console.error(err));
                        }
                    }

                    MenuItem {
                        text: "About..."
                    }

                    MenuSeparator { }

                    MenuItem {
                        text: "Accept Guest Code"
                        onClick: () =>
                        {
                            const dlg = doc.acceptGuestDialogT();
                            dlg.show();
                        }
                    }

                    MenuItem {
                        text: "Log Out"
                        onClick: () =>
                        {
                            httpRequest("/logout").send()
                            .then(() => { window.location.reload(); })
                            .catch(err => { });
                        }
                    }

                }
            }

            Box { fill: true }

            Label {
                marginLeft: theme.paddingMedium
                text: "[icon:comm-person] " + escapeMarkup(doc.user)
            }

            TimeLabel {
                marginLeft: theme.paddingMedium
            }

            Button {
                width: bboxHeight
                fillHeight: true
                flat: true
                icon: documentRoot.fullscreenItem ? "ui-fullscreen_exit"
                                                  : "ui-fullscreen"

                onClick: () =>
                {
                    if (documentRoot.fullscreenItem)
                    {
                        documentRoot.fullscreenItem = null;
                    }
                    else
                    {
                        documentRoot.fullscreenItem = documentRoot;
                    }
                }
            }
        }

        // content area
        Loader {
            fill: true

            sourceTemplate: ! doc.loggedIn && ! doc.logInPending ? loginScreenT
                                                                 : ! doc.logInPending ? filesBoxT
                                                                                      :  null
        }
    }

    NotificationArea {
        id: notificationArea
        
        position: "global"
        origin: "top-right"
        width: theme.itemWidthLarge * 2
        fillHeight: true
    }


    // welcome screen with login options
    property loginScreenT: template Box {
        fill: true
        layout: "center-column"

        Label {
            visible: ! codeLabel.visible
            fontSize: theme.fontSizeLarge
            text: "Pilvini Private Cloud Drive"
        }

        Label {
            visible: ! codeLabel.visible
            marginBottom: theme.paddingLarge
            fontSize: theme.fontSizeSmall
            color: theme.secondaryColor
            text: "Copyright (c) 2017 - 2023 by Martin Grimme"
        }

        Button {
            visible: ! codeLabel.visible
            marginTop: theme.paddingLarge
            flat: true
            text: "Login"

            onClick: () =>
            {
                httpRequest("/login").send()
                .then(req =>
                {
                    if (req.ok)
                    {
                        doc.loggedIn = true;
                    }
                })
                .catch(err =>
                {
                    alert(err);
                });
            }
        }

        Button {
            visible: ! codeLabel.visible
            marginTop: theme.paddingLarge
            flat: true
            text: "Request Guest Access"

            onClick: () =>
            {
                const code = Math.round(Math.random() * (9999 - 1000) + 1000);
                codeLabel.text = code;
                httpRequest("/login?code=" + code).send()
                .then(req =>
                {
                    codeLabel.text = "";
                    if (req.ok)
                    {
                        doc.loggedIn = true;
                    }
                })
                .catch(err =>
                {
                    alert("Login Failed.");
                });
            }
        }

        Label {
            visible: codeLabel.visible
            fontSize: theme.fontSizeLarge
            text: "Awaiting Authorization for Code"
        }

        Label {
            id: codeLabel

            visible: text !== ""
            marginTop: theme.paddingLarge
            fontSize: theme.fontSizeLarge * 2
        }
    }

    property registryDialogT: template RegistryDialog { }

    property acceptGuestDialogT: template Dialog {
        id: dialog

        title: "Enter Guest Code"

        CodePad {
            width: 300
            height: 340

            onCodeEntered: code =>
            {
                httpRequest("/acceptLogin?code=" + code).send();
                dialog.parent = null;
            }
        }
    }

    property filesBoxT: template Box {

        fill: true
        
        layout: bboxWidth > bboxHeight ? "row" : "column"

        onInitialization: () =>
        {
            console.log("Create File Manager");
        }
        
        ListModel {
            id: tabsModel

            data: [
                { path: "/" }
            ]
        }

        Repeater {
            model: tabsModel

            delegate: template FilesBox {
                fill: true
                fs: remoteFs
                path: modelData.value.path
                mayClose: tabsModel.size > 1

                onOpenNew: (fs, path) =>
                {
                    tabsModel.insert(tabsModel.size, { path: path });
                }

                onClose: () =>
                {
                    tabsModel.remove(modelData.index);
                }
            }
        }
    }

}