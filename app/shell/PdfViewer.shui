require "shellfish/ui";
require "./inertialengine.js";
require "./PageBrowser.shui";
require "./PageItem.shui";
require "./pdfdocument.js";
require "./pdfpage.js";
require "./ViewportBox.shui";

Box {
    id: box

    property source: ""
    property title: ""

    color: "black"

    PDFDocument {
        id: pdfDoc

        source: box.source
    }

    PageBrowser {
        id: browser

        fillWidth: true
        fillHeight: true

        model: pdfDoc.pages
        current: 0

        delegate: template PageItem {
            id: pageItem

            pageBrowser: browser

            PDFPage {
                id: pdf

                property vpScale: pageItem.viewport.scale

                document: pdfDoc
                page: pageItem.modelData.index

                visible: status === "success"

                position: "free"
                x: -pageItem.viewport.viewX * pageItem.viewport.scale
                y: -pageItem.viewport.viewY * pageItem.viewport.scale
                width: originalWidth * pageItem.viewport.scale
                height: originalHeight * pageItem.viewport.scale
                scale: 1.0

                onStatusChanged: () =>
                {
                    if (status === "success")
                    {
                        pageItem.viewport.originalWidth = originalWidth;
                        pageItem.viewport.originalHeight = originalHeight;
                        pageItem.viewport.scaleToFit();
                    }
                }

                onVpScaleChanged: () =>
                {
                    browser.scrollable = pageItem.viewport.scale < pageItem.viewport.minScale + 0.0001;
                }
            }

            Label {
                visible: ! pdf.visible
                position: "free"
                x: (parent.bboxWidth - bboxWidth) / 2
                y: (parent.bboxHeight - bboxHeight) / 2
                fontSize: theme.fontSizeLarge
                text: "[icon:ui-spinner5]"
            }

        }
    }

}
