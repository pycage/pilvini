require "shellfish/ui";

Box {
    id: pad

    property cellWidth: Math.ceil(bboxWidth / 3)
    property cellHeight: Math.ceil(bboxHeight / 5)

    event codeEntered

    layout: "row"
    overflowBehavior: "wrap"

    Object {
        id: priv
        property code: ""
    }

    Box {
        width: cellWidth * 3
        height: cellHeight

        Box {
            fillWidth: true
            fillHeight: true
            marginBottom: theme.paddingSmall
            color: theme.secondaryBackgroundColor
            layout: "center-row"

            Label {
                fillWidth: true
                marginLeft: theme.paddingSmall
                marginRight: theme.paddingSmall
                horizontalAlignment: "right"
                fontSize: theme.fontSizeLarge
                fontFamily: "Courier New"
                text: priv.code
            }
        }
    }

    Repeater {
        model: ListModel {
            data: [
                "7", "8", "9",
                "4", "5", "6",
                "1", "2", "3",
                "0", "<", "C"
            ]
        }

        delegate: template Box {
            width: pad.cellWidth
            height: pad.cellHeight

            Button {
                fillWidth: true
                fillHeight: true
                marginTop: 2
                marginLeft: 2
                marginRight: 2
                marginBottom: 2

                text: parent.modelData.value

                onClick: ev =>
                {
                    ev.accepted = true;

                    if (text === "<")
                    {
                        if (priv.code.length > 0)
                        {
                            priv.code = priv.code.substring(0, priv.code.length - 1);
                        }
                    }
                    else if (text === "C")
                    {
                        priv.code = "";
                    }
                    else
                    {
                        priv.code += text;
                    }

                    if (priv.code.length === 4)
                    {
                        wait(500).then(() =>
                        {
                            pad.codeEntered(priv.code);
                        });
                    }
                }
            }
        }
    }

}