require "shellfish/low" as low;
require "shellfish/ui";
require "./ImageViewer.shui";
require "./MediaPlayer.shui";
require "./PdfViewer.shui";
require "./ViewportBox.shui";
require "./youtubevideo.js";

Box {
    id: box

    property windowIcon: "media-image"
    property windowTitle: loader.fileItem ? loader.fileItem.name : ""

    property files: []

    property fs: null
    property model: ListModel { data: files }
    property current: -1
    property maximized: false

    borderRadius: maximized ? 0 : 6
    color: theme.secondaryBackgroundColor

    event toggleMaximized
    event swapPanes
    event closePane

    fillWidth: true
    fillHeight: true

    DropArea {
        fillWidth: true
        fillHeight: true
        marginTop: box.maximized ? 0 : theme.itemHeightMedium

        onDropAccept: ev =>
        {
            if (ev.types.includes("text/x-fileitems"))
            {
                ev.accepted = true;
            }
        }

        onDrop: ev =>
        {
            const items = JSON.parse(ev.data["text/x-fileitems"]);
            box.model.reset(items);
            box.current = 0;
        }

        Loader {
            id: loader

            property fileItem: box.current !== -1 && box.model.size > box.current ? box.model.at(box.current)
                                                                                  : null

            fillWidth: true
            fillHeight: true

            sourceTemplate: (() =>
            {
                if (! fileItem)
                {
                    return null;
                }

                const item = fileItem;
                if (item.mimetype === "application/pdf")
                {
                    return pdfViewerT;
                }
                else if (item.mimetype.startsWith("image/"))
                {
                    return imageViewerT;
                }
                else if (item.mimetype.startsWith("audio/") ||
                        item.mimetype.startsWith("video/"))
                {
                    return mediaPlayerT;
                }
                else if (item.mimetype.startsWith("text/"))
                {
                    console.log("TEXT: " + item.path);
                    return textViewerT;
                }
                else if (item.mimetype === "application/x-youtube-link")
                {
                    return youTubeViewerT;
                }
                else
                {
                    return null;
                }
            })()
        }

    }

    MouseBox {
        opacity: box.maximized && ! containsMouse ? 0 : 1

        position: "free"
        fillWidth: true
        height: theme.itemHeightMedium
        color: theme.secondaryBackgroundColor

        layout: "center-row"

        onDoubleClick: ev =>
        {
            box.toggleMaximized();
            ev.accepted = true;
        }

        Label {
            marginLeft: theme.paddingSmall
            marginRight: theme.paddingSmall
            fillWidth: true
            overflowBehavior: "ellipsis"
            bold: true
            text: loader.fileItem && loader.fileItem.name ? low.escapeMarkup(loader.fileItem.name) : ""
        }

        Button {
            visible: ! box.maximized
            fillHeight: true
            flat: true
            icon: "ui-arrow_left"

            onClick: () =>
            {
                box.swapPanes(-1);
            }
        }

        Button {
            visible: ! box.maximized
            fillHeight: true
            flat: true
            icon: "ui-arrow_right"

            onClick: () =>
            {
                box.swapPanes(1);
            }
        }

        Button {
            fillHeight: true
            flat: true
            icon: "ui-clear"

            onClick: () =>
            {
                box.closePane();
            }
        }
    }

    property imageViewerT: template ImageViewer {
        fillWidth: true
        fillHeight: true

        model: box.model
        current: box.current
        source: loader.fileItem ? loader.fileItem.path : ""
    }

    property pdfViewerT: template PdfViewer {
        fillWidth: true
        fillHeight: true
        source: loader.fileItem ? loader.fileItem.path : ""
    }

    property mediaPlayerT: template MediaPlayer {
        fillWidth: true
        fillHeight: true

        minimized: false

        model: box.model
        current: box.current

        onCurrentChanged: () =>
        {
            if (box.current !== current)
            {
                box.current = current;
            }
        }
    }

    property youTubeViewerT: template YouTubeVideo {
        fillWidth: true
        fillHeight: true

        property path: loader.fileItem ? loader.fileItem.path : ""

        onPathChanged: () =>
        {
            box.fs.read(path)
            .then(blob => blob.text())
            .then(json =>
            {
                const obj = JSON.parse(json);
                videoId = obj.videoId;
            })
            .catch(err => { });
        }
    }

    property textViewerT: template Box {
        fillWidth: true
        fillHeight: true

        overflowBehavior: "scroll"

        Label {
            property source: loader.fileItem ? loader.fileItem.path : ""

            fillWidth: true
            overflowBehavior: "wrap"
            literal: true
            fontFamily: "Monospace"

            onSourceChanged: () =>
            {
                if (source === "")
                {
                    text = "";
                }
                else
                {
                    console.log("load " + source);
                    window.fetch(source)
                    .then(r => r.text())
                    .then(safeCallback(d => text = d))
                    .catch(err => { console.error(err); });
                }
            }
        }
    }
}